plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.hyuny'
version = '0.0.1-SNAPSHOT'

// 하위 모듈 전용 설정
subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation platform('org.springframework.boot:spring-boot-dependencies:3.4.5')
        compileOnly 'org.projectlombok:lombok:+'
        annotationProcessor 'org.projectlombok:lombok:+'
    }

    if (project.name == 'app' ) {
        apply plugin: 'org.springframework.boot'
        tasks.named('bootJar') {enabled = true}
        tasks.named('jar') {enabled = false}
    } else {
        tasks.named('jar') {
            enabled = true
            archiveClassifier = null}
    }

    tasks.withType(Jar).configureEach {
        destinationDirectory.set(rootProject.layout.buildDirectory.dir("libs"))
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('bootJar') {
    enabled = false
}

tasks.named('jar') {
    enabled = false
}
